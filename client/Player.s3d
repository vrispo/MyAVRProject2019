#include "Bullet.s3d"

#define SWAT_START_WALK 16
#define SWAT_END_WALK 48
#define COLLISION_SCALE 0.90, 1.6, 0.90
#define Z_COLLISION_OFF 0.0
#define X_COLLISION_OFF 0.0

class Player{
	var lower_mesh;
	var upper_mesh;
	var collision_box;
	var weapon_mesh;
	var obj;
	var collision_obj;
	var position;
	var angle;
	var direction;
	var step;
	var frame_step;
	var current_frame;
	var run;
	var my_bullet;
	var has_shot;
	var color;
	var shoot_fx;
	var step_fx;
	
	init(col);
	draw();
	setposition(pos);
	setrotation(angle);
	update(level_obj_l);
};

function Player::setrotation(angle){
	obj.setrotation(angle * 180.0 / PI, 0, 1, 0);
}

function Player::init(col){
	color = col;
	lower_mesh = CVmNewMesh("swat_lower.aam");
	var sf1 = lower_mesh.normalize(1);
	upper_mesh = CVmNewMesh("swat_upper.aam");
	upper_mesh.scale(sf1);
	collision_box = CVmNewMesh(VRP_CYLINDER);
	collision_box.scale(COLLISION_SCALE);
	var collision_box_transparent = CVmMaterial();
	collision_box_transparent.transparency = 1.0;
	collision_box.setMaterial(collision_box_transparent);
	weapon_mesh = CVmNewMesh("rocketl.aam");
	weapon_mesh.scale(sf1);
	
	var lower_obj = CVmObj(lower_mesh);
	var upper_obj = CVmObj(upper_mesh);
	var weapon_obj = CVmObj(weapon_mesh);
	collision_obj = CVmObj(collision_box);
	obj = CVmObj();
	obj.addchild(upper_obj);
	obj.addchild(lower_obj);
	obj.addchild(weapon_obj);
	collision_obj.setpivotpoint(Z_COLLISION_OFF, 0, X_COLLISION_OFF);
	
	obj.modulatematerials(color);	
	
	angle = 0;
	direction = [0,0,1];
	step = 0.1;
	frame_step = 1.0;
	current_frame = SWAT_START_WALK;
	run = 1;
	
	has_shot = false;
	shoot_fx = CVmVRawAV("shoot.wav");
	step_fx = CVmVRawAV("step.wav");
	step_fx.play(); //So it doesn't block
}

function Player::draw(){
	obj.draw(VR_FRAMENUMBER, current_frame);
	collision_obj.draw();
	if (has_shot)
		my_bullet.draw();
}

function Player::setposition(pos){
	position = pos;
	obj.setposition(position);
	collision_obj.setposition(position + [-Z_COLLISION_OFF, 0, -X_COLLISION_OFF]);
}

function Player::update(level_obj_l){
	var old_position = position;
	
	if (keyPressed(VK_SHIFT)){
		run = 2;
	} else {
		run = 1;
	}
	
	if (keyPressed(VK_RIGHT))
		angle -= 0.01;
	else if (keyPressed(VK_LEFT))
		angle += 0.01;
	
	direction = [sin(angle), 0, cos(angle)];
	
	if (keyPressed(VK_UP)){
		position += (direction * step * run);
		current_frame += frame_step;
		if (current_frame == SWAT_END_WALK){
			current_frame = SWAT_START_WALK + 1;
			step_fx.play();
		}		
	}		
	else if (keyPressed(VK_DOWN)){
		position -= (direction * step);
		current_frame -= frame_step;
		if(current_frame == SWAT_START_WALK)
			current_frame = SWAT_END_WALK - 1;
			step_fx.play();		
	}

		
	setrotation(angle);
	setposition(position);
	
	var i = int(position.z);
	var j = int(position.x);
	var k = int(position.y);
	var collision_check = false;
	foreach (var t in level_obj_l)
		collision_check = collision_check || collision_obj.isCollidingBBox(t);
	if (collision_check)
		position = old_position;
		
	if (keyPressed(VK_SPACE) && !has_shot){
		my_bullet = bullet();
		my_bullet.init(position, direction);
		shoot_fx.play();
		has_shot = true;
	}
	if (has_shot)
		my_bullet.update();
}